!! =================================== !
!! The Old French morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ç

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ç

%{T%}:t
%{T%}:0

%{D%}:d
%{D%}:0

%{S%}:s
%{S%}:0

%{H%}:h
%{H%}:0

 %>:0 ;

 

Sets

 Vow = a e i o u y æ 
       á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ;
 Cns = b c d f g h j k l m n p q r s t v w x z ;
 
Rules

!! t+s and st+s become z
"s becomes z after a t"
s:z <=> t: %>: _ ;

"t disappears before an s becoming a z"
t:0 <=> _ %>: s: ;

"s disappears before a ts that becomes a z"
s:0 <=> _ t:0 %>: s:z ;

!! {T}+s and s{T}+s become z
"{T} becomes 0 before an s"
%{T%}:0 <=> _ s: ;

"s can become z when it occurs after {T}"
s:z => %{T%}: _ ;

!!"s can become s when it occurs after {T}"
!!s:s => %{T%}: _ ; BREAKS EVERYTHING

"s disappears before a {T}s that becomes a z"
s:0 <=> _ %{T%}:0 s:z ;

!! ds becomes z
"s becomes z after a d"
s:z <=> d: %>: _ ;

"d disappears before an s becoming a z"
d:0 <=> _ %>: s:z ;

!! p, f, c, b, and v disappear before inflectional -s
!! FIXME: change to labials + c
"p disappears before inflectional -s"
p:0 <=> _ %>: s: ;

"f disappears before inflectional -s"
f:0 <=> _ %>: s: ;

"c disappears before inflectional -s"
c:0 <=> _ %>: s: ;

"b disappears before inflectional -s"
b:0 <=> _ %>: s: ;

"v disappears before inflectional -s"
v:0 <=> _ %>: s: ;

!! nasal alterations

"after a vowel, m+s becomes ns"
m:n <=> Vow _ s:s ;

"rm+s becomes rs"
m:0 <=> r:r _ s:s ;

"rn+s becomes rz"
s:z <=> r:r n:0 _ ;

"rn+s becomes rz"
n:0 <=> r:r _ s:z ;

"n'+s (gn+s) becomes nz"
s:z <=> g:0 n:n _ ;

"n'+s (gn+s) becomes nz"
g:0 <=> _ n:n s:z ;

"n'+s (ng+s) becomes nz"
s:z <=> n:n g:0 _ ;

"n'+s (ng+s) becomes nz"
g:0 <=> n:n _ s:z ;

!! Silent t and d
"unaspirated t can be kept"
%{T%}:t => _ ;

"unaspirated t can be dropped"
%{T%}:0 => _ ;

"unaspirated d can be kept"
%{D%}:d => _ ;

"unaspirated t can be dropped"
%{D%}:0 => _ ;

!! Palatalized vowels
"i can be expressed as y after vowels"
i:y => Vow _ ;

"e can be expressed as o before i/y"
e:o => _ i: ;

"ii can be shortened to i" 
i:0 => i: _ ;

!! Ordinals
"s can become è in the ordinal suffix"
s:è => i _ m e ;

"e can be inserted before s in the ordinal suffix"
0:e => i _ s:s m e ;

"optional s can be deleted"
%{S%}:0 => _ ;

"optional s can be kept"
%{S%}:s => _ ;

"optional h can be deleted"
%{H%}:0 => _ ;

"optional h can be kept"
%{H%}:h => _ ;
